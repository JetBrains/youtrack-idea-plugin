plugins {
    id 'org.jetbrains.intellij' version '1.13.0'
    id 'org.jetbrains.kotlin.jvm' version '1.8.0'
    id 'idea'
}

allprojects {

    group 'com.github.jk1'
    sourceCompatibility = 17
    targetCompatibility = 17

    if (project.hasProperty('teamcity')) {
        project.version = project.teamcity['build.number']
    } else {
        version '9999.9.9' // dev version
    }
    repositories {
        mavenCentral()
    }
}

// TODO: restore 2022.3 and TRUNK tests in one run
def supportedVersions = ['IU-2022.3']

intellij {
    pluginName = 'ytplugin'
    version = project.hasProperty('ideaVersion') ? project.ideaVersion : 'IU-223.8617.56'
    println "This build uses IDEA Ultimate ${version.get()}"
    try {
        plugins = ['JavaScript', 'JavaScriptDebugger', 'tasks']
    } catch (ignored){
        plugins = ['tasks']
    }
    updateSinceUntilBuild = false
    instrumentCode = false
}


idea {
    module {
        downloadSources = true
        outputDir file("$buildDir/idea/main")
        testOutputDir file("$buildDir/idea/test")
    }
}

compileKotlin {
    kotlinOptions {
        jvmTarget = '17'
        apiVersion = '1.6'
        languageVersion = '1.7'
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = '17'
        apiVersion = '1.6'
        languageVersion = '1.7'
    }
}

publishPlugin {
    token = project.findProperty("marketplaceToken") ?: ""
    channels = ['stable']
}

dependencies {
    implementation project(':api')
    implementation "org.nanohttpd:nanohttpd:2.3.0"
//    implementation "org.apache.httpcomponents:httpclient:4.5.13"

    testImplementation 'junit:junit:4.12'
}

test {
    testLogging {
        showStandardStreams = true
        exceptionFormat = 'full'
    }
    outputs.upToDateWhen { false }
}

// runs `./gradlew test` several times against all supported IDE versions
task testAllSupportedIdes {
    def taskNames = supportedVersions.collect { "runTestsWithIdea$it" }
    dependsOn taskNames.first()
    supportedVersions.eachWithIndex { version, index ->
        task "runTestsWithIdea$version"(type: GradleBuild) {
            if (index != supportedVersions.size() - 1) {
                dependsOn taskNames.get(index + 1)
            }
            buildName = "runTestsWithIdea$version"
            buildFile = 'build.gradle'
            tasks = ['test']
            startParameter.projectProperties = [ideaVersion: version]
        }
    }
}

// Travis CI
check.dependsOn testAllSupportedIdes

